/*
	A tip calculation FIS (fuzzy inference system)
	Calculates tip based on 'service', 'food' quality and 'price' pricing
*/

FUNCTION_BLOCK tipper	// Block definition (there may be more than one block per file)

// Define input variables
VAR_INPUT
	service : REAL;
	food : REAL;
	price : REAL;
END_VAR

// Define output variable
VAR_OUTPUT
	tip : REAL;
END_VAR

// Fuzzify input variable 'service': {'poor', 'good' , 'excellent'}
FUZZIFY service
	TERM poor := (0, 1) (4, 0) ;
	TERM good := (1, 0) (4,1) (6,1) (9,0);
	TERM excellent := (6, 0) (9, 1);
END_FUZZIFY

// Fuzzify input variable 'food': { 'rancid', 'delicious' }
FUZZIFY food
	TERM rancid := (0, 1) (1, 1) (3,0) ;
	TERM delicious := (7,0) (9,1);
END_FUZZIFY

//Fuzzify input variable 'price' : { 'cheap', 'average', 'expensive' }
FUZZIFY price
    TERM cheap := (0,0) (25,1) (50,0);
    TERM average := (40,0) (75,1) (100,0);
    TERM expensive := (90,0) (125,1) (150,0);
END_FUZZIFY

// Defzzzify output variable 'tip' : {'cheap', 'average', 'generous' }
DEFUZZIFY tip
	TERM cheap := (0,0) (5,1) (10,0);
	TERM average := (10,0) (15,1) (20,0);
	TERM generous := (20,0) (25,1) (30,0);
	METHOD : COG;		// Use 'Center Of Gravity' defuzzification method
	DEFAULT := 0;		// Default value is 0 (if no RULE activates defuzzifier)
END_DEFUZZIFY

// Inference RULEs
RULEBLOCK No1
	AND : MIN;	// Use 'min' for 'and'
	ACT : MIN;	// Use 'min' activation method
	ACCU : MAX;	// Use 'max' accumulation method

    RULE 1  : IF service IS poor OR food IS rancid AND price IS cheap THEN tip IS cheap;
    RULE 2  : IF service IS poor OR food IS rancid AND price IS average THEN tip IS cheap;
    RULE 3  : IF service IS poor OR food IS rancid AND price IS expensive THEN tip IS cheap;
    RULE 4  : IF service IS good OR food IS delicious AND price IS expensive THEN tip IS average;
    RULE 5  : IF service IS excellent OR food IS delicious AND price IS cheap THEN tip IS generous;
    RULE 6  : IF service IS excellent OR food IS delicious AND price IS average THEN tip IS generous;
    RULE 7  : IF service IS excellent OR food IS delicious AND price IS expensive THEN tip IS generous;
END_RULEBLOCK

END_FUNCTION_BLOCK